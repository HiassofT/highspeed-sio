;  highsiodet.src - check for highspeed drives
;
;  Copyright (c) by ABBUC e.V. (www.abbuc.de) and Matthias Reichl
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
;

HISIO	LDA DUNIT
	BNE ?DOSIO	; DUNIT = 0 clears speed table

	LDX #8
?CLRLP	STA SPEEDTB-1,X
	DEX 
	BNE ?CLRLP
	RTS 

?DOSIO	LDX DUNIT
	LDA SPEEDTB-1,X
	BEQ ?DOCHK

.IF .DEF USENOHIOS
	CMP #STDSPD
	BNE ?NOSTD
	JMP $E459
?NOSTD	= *
.ENDIF
	STA SIOSPEED
	JMP DOHISIO

?DOCHK	
	LDA SOUNDR
	PHA
	LDA #0
	STA SOUNDR

	LDA #STDSPD
	STA SIOSPEED
	LDA DAUX2
	PHA
	LDA DAUX1
	PHA
	LDY #$07
?SAVCMD	LDA DCOMND,Y
	PHA 
	LDA ?CMD3F,Y	; Happy/Speedy Ultra SIO mode?
	STA DCOMND,Y
	DEY 
	BPL ?SAVCMD

	LDA #1
	JSR DOHIDET

	; for testing happy 810 mode, uncomment this and connect a happy 1050
	;LDY #$FF

	BMI ?NOULTRA
	LDX DUNIT
	LDA HIBUF
	STA SPEEDTB-1,X
	JMP ?RESCMD

?NOULTRA LDA #TURFLG	; Turbo 1050?
	STA SIOSPEED
	JSR ?GETST
	BPL ?SETSPD

	LDA #XFFLG	; XF551?
	STA SIOSPEED
	JSR ?GETST
	BPL ?SETSPD

	LDA #HAPFLG	; Happy 810?
	STA SIOSPEED
	JSR ?CHKHAP
	BPL ?SETSPD

	LDA #STDSPD	; no, just use standard SIO speed
	BNE ?SETSP2

?GETST	LDY #$07	; try to get status
?GETSTL	LDA ?CMDST,Y
	STA DCOMND,Y
	DEY
	BPL ?GETSTL
	LDA #1
	JSR DOHIDET
	LDY DSTATS
	RTS

?CHKHAP	LDA #$48	; check for happy 810
	STA DCOMND
	LDA #0
	STA DSTATS
	STA DAUX2
	LDA #$20
	STA DAUX1
	LDA #1
	JSR DOHIDET
	RTS

?SETSPD	LDA SIOSPEED
?SETSP2	LDX DUNIT	
	STA SPEEDTB-1,X

?RESCMD	LDY #$00
?RESLP	PLA 
	STA DCOMND,Y
	INY 
	CPY #$0A
	BCC ?RESLP
	PLA
	STA SOUNDR
	JMP ?DOSIO

?CMD3F	.BYTE $3F,$40
	.WORD HIBUF,1,1

?CMDST	.BYTE $53,$40
	.WORD HIBUF,1,4

