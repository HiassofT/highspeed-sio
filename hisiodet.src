;  highsiodet.src - check for highspeed drives
;
;  Copyright (c) by ABBUC e.V. (www.abbuc.de) and Matthias Reichl
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
;

HISIO	LDA DUNIT
	BNE ?DOSIO	; DUNIT = 0 clears speed table

	LDX #8
?CLRLP	STA SPEEDTB-1,X
	DEX 
	BNE ?CLRLP
	RTS 

?DOSIO	LDX DUNIT
	LDA SPEEDTB-1,X
	BEQ ?DOCHK

.IF .DEF USENOHIOS
	CMP #STDSPD
	BNE ?NOSTD
	JMP $E459
?NOSTD	= *
.ENDIF
	STA SIOSPEED
	JMP DOHISIO

?DOCHK	
	LDA SOUNDR
	PHA
	LDA #0
	STA SOUNDR

	LDA #STDSPD
	STA SIOSPEED
	LDY #$0B
?SAVCMD	LDA DCOMND,Y
	PHA 
	LDA ?CMD3F,Y	; Happy/Speedy Ultra SIO mode?
	STA DCOMND,Y
	DEY 
	BPL ?SAVCMD

	JSR ?DODET

	; for testing happy 810 mode, uncomment this and connect a happy 1050
	;LDY #$FF

	BMI ?NOULTRA

	LDA HIBUF
	CMP #$0A	; speed byte $0A is most certainly a Happy 1050
	BNE ?SETSP2

	; enable fast write for Happy 1050 (but not for other drives),
	; otherwise data might be corrupted
	; stupid, stupid Happy...
	JSR ?CHKHAP

	LDA HIBUF
	JMP ?SETSP2

?NOULTRA LDA #TURFLG	; Turbo 1050?
	JSR ?GETST
	BPL ?SETSPD

	LDA #XFFLG	; XF551?
	JSR ?GETST
	BPL ?SETSPD

	LDA #HAPFLG	; Happy 810?
	STA SIOSPEED
	JSR ?CHKHAP
	BPL ?SETSPD

	LDA #STDSPD	; no, just use standard SIO speed
	BNE ?SETSP2

?GETST	STA SIOSPEED
	LDA #4
	STA DBYTLO
	LDX #$53
	LDA #$40
?DOIMM	STX DCOMND
	STA DSTATS
?DODET	LDA #1
	JMP DOHIDET

?CHKHAP	LDX #$48	; check for happy 810
	LDA #0
	BEQ ?DOIMM

?SETSPD	LDA SIOSPEED
?SETSP2	LDX DUNIT	
	STA SPEEDTB-1,X

?RESCMD	LDY #$00
?RESLP	PLA 
	STA DCOMND,Y
	INY 
	CPY #$0C
	BCC ?RESLP
	PLA
	STA SOUNDR
	JMP ?DOSIO

?CMD3F	.BYTE $3F,$40
	.WORD HIBUF,1,1,$20
