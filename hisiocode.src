;  highsiocode.src - highspeed SIO routine
;
;  Copyright (c) by ABBUC e.V. (www.abbuc.de) and Matthias Reichl
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
;

DOHISIO	SEI
	INC CRITIC
	LDA DUNIT
	ORA #$30
	STA CDEVIC
	LDA DCOMND
	STA CCOMND
	LDA DAUX1
	STA CAUX1
	LDA DAUX2
	STA CAUX2

.if .def DIAGSIO
	LDA #0
	STA ?DIAGER
.endif
	LDA SIOSPEED
	BPL ?NOTUR

	LDA CAUX2
	ORA #$80
	STA CAUX2
	BNE ?DOSIO1

?NOTUR	CMP #HAPFLG
	BEQ ?HAPPY
	CMP #XFFLG
	BNE ?DOSIO1

?XFCMD	LDA CCOMND
	ORA #$80
	STA CCOMND
	CMP #$A1
	BEQ ?DOSLOW
	CMP #$A2
	BNE ?DOSIO1
?DOSLOW	LDA #STDSPD
	STA SIOSPEED
	BNE ?DOSIO1

?HAPPY	LDA CCOMND
	CMP #$50
	BEQ ?HAPPY1
	CMP #$52
	BEQ ?HAPPY1
	CMP #$57
	BNE ?DOSLOW
?HAPPY1	ORA #$20
	STA CCOMND

?DOSIO1	TSX 
	STX FEOF

.if .def DIAGSIO
	LDA #1
.else
	LDA #$02
.endif
	STA DRETRY
?DRETLP	LDA #$07
	STA CRETRY
?CRETLP	
.if .def DIAGSIO
	LDA #0
	STA ?DIAGST
	STA ?DIAGBT
.endif
	LDA SIOSPEED
	CMP #$40
	BCC ?DOSIO2
	LDA #STDSPD
?DOSIO2	STA AUDF3
	LDA #$34
	STA PBCTL
	LDA #$00
	STA STATUS
	STA FTYPE
	STA BFENHI
	STA AUDF4
	LDA #$3A
	STA BUFRLO
	LDA #$02
	STA BUFRHI
	ASL 
	STA BFENLO

	JSR ?SENDBLK

	BIT SIOSPEED
	BPL ?NOTUR1
	LDA #TURSPD
	STA AUDF3
?NOTUR1	JSR ?CHKST

.if .def DIAGSIO
	INC ?DIAGST
.endif

	BIT SIOSPEED
	BVC ?NOXF
	LDA #XFSPD
	STA AUDF3
?NOXF	LDA DBUFLO
	STA BUFRLO
	LDA DBUFHI
	STA BUFRHI
	LDA DBYTLO
	STA BFENLO
	LDA DBYTHI
	STA BFENHI
	LDA DSTATS
	BPL ?NOSEND
	JSR ?SENDBLK
	JSR ?CHKST
?NOSEND	DEC FTYPE
	JSR ?SETTIM1
	BIT DSTATS
	BVC ?NOGET
	JSR ?GETBLK
?NOGET	LDA #$A0
	STA AUDC4
	LDA POKMSK
	STA IRQEN
	JSR ?CLRTIM1
	LDA STATUS
	BEQ ?ENDCMD
	DEC DRETRY
	BEQ ?ENDCMD
	JMP ?DRETLP
?ENDCMD	DEC CRITIC
	TAY
	BNE ?ERRCMD
	INY
?ERRCMD	STY DSTATS
	CLI 
	RTS 

?SENDBLK	LDY #$00
?WT1	INY 
	BNE ?WT1
	LDA #$23
	JSR ?SETPOKY
	LDA (BUFRLO),Y
	STA CHKSUM
	STA SEROUT
	INY 
	BNE ?SND2
?SND1	LDA (BUFRLO),Y
	JSR ?SENDBYT
	INY 
	BNE ?SND2
	INC BUFRHI
	DEC BFENHI
	LDX #$E0
?WT2	INX 
	BNE ?WT2
?SND2	CPY BFENLO
	BNE ?SND1
	LDA BFENHI
	BNE ?SND1
	LDA CHKSUM
	JSR ?SENDBYT
?WTSND	LDA IRQST
	AND #$08
	BNE ?WTSND
	RTS

?CHKST	LDY #$03
	JSR ?STIMOUT
	LDA #$C0
	STA IRQEN
	BNE ?RDQUIT

?GETBLK	LDY #$00
	STY CHKSUM
?GETLP	JSR ?GETBYTE
	STA (BUFRLO),Y
	JSR ?ADDSUM
	INY 
	BNE ?GET1
	INC BUFRHI
	DEC BFENHI
?GET1	CPY BFENLO
	BNE ?GETLP
	LDA BFENHI
	BNE ?GETLP
	JSR ?GETBYTE
.if .def DIAGSIO
	STA ?DIAGBT
.endif
	CMP CHKSUM
	BNE ?ERR143
	RTS 

?SETTIM1	LDA DTIMLO
	ROR 
	ROR 
	TAY 
	AND #$3F
	TAX 
	TYA 
	ROR 
	AND #$C0
	TAY 
	JSR ?STIMOU2
?RDQUIT	LDA #$13
	JSR ?SETPOKY
	LDA #$3C
	STA PBCTL
	JSR ?GETBYTE
.if .def DIAGSIO
	STA ?DIAGBT
.endif
	CMP #$41
	BEQ ?CLRTIM1
	CMP #$43
	BEQ ?CLRTIM1
	CMP #$45
	BEQ ?ERR144
	LDA #$8B
	BNE ?ERR
?ERR144	LDA #$90
	STA STATUS

?CLRTIM1	LDY #$00
?STIMOUT	LDX #$00
?STIMOU2	LDA #<?ERR138
	STA CDTMA1
	LDA #>?ERR138
	STA CDTMA1+1
	STY CDTMV1
	STX CDTMV1+1
	RTS
;	LDA #1
;	JMP SETVBV


?ERR128	LDX FEOF
	TXS 
	LDA #$80
	STA STATUS
	BNE ?ERR1
?ERR143	LDA #$8F
	.BYTE $2C
?ERR138	LDA #$8A
?ERR	STA STATUS
.if .def DIAGSIO
	LDX ?DIAGER
	STA ?DIAGE,X
	LDA ?DIAGST
	STA ?DIAGS,X
	LDA ?DIAGBT
	STA ?DIAGB,X
	INC ?DIAGER
.endif
	LDX FEOF
	TXS 
	LDA FTYPE
	BMI ?ERR1
	DEC CRETRY
	BEQ ?ERR1
	JMP ?CRETLP
?ERR1	JMP ?NOGET

?GETBYTE LDA #$20
?GETBY1	BIT IRQST
	BPL ?ERR128
	BNE ?GETBY1
	LDA #$DF
	STA IRQEN
	LDA #$E0
	STA IRQEN
	LDA SKSTAT
	STA SKREST
	BPL ?ERR138
	AND #$20
	BEQ ?ERR138
	LDA SERIN
	RTS 

?SENDBYT	TAX 
?WPUT	LDA IRQST
	AND #$10
	BNE ?WPUT
	LDA #$EF
	STA IRQEN
	LDA #$D0
	STA IRQEN
	TXA 
	STA SEROUT
	LDX IRQST
	BPL ?ERR128

?ADDSUM	CLC 
	ADC CHKSUM
	ADC #$00
	STA CHKSUM
	RTS 

?SETPOKY STA SKCTL
	STA SKREST
	LDA #$28
	STA AUDCTL
	LDA SOUNDR
	BEQ ?NOSND
	LDA #$A8
	BNE ?DOSND
?NOSND  LDA #$A0
?DOSND	STA AUDC4
	LDA #$F8
	STA IRQEN
	RTS 

.if .def DIAGSIO
?DIAGS	.DC 7 0
?DIAGB	.DC 7 0
?DIAGE	.DC 7 0
?DIAGBT	.BYTE 0
?DIAGST	.BYTE 0
?DIAGER	.DC 7 0

PDIAG	LDA ?DIAGER
	BEQ ?PDIAG0

	LDA #0
	STA ?DIAGBT
?PDIAG1	LDX ?DIAGBT
	CPX #$0C
	BEQ ?PDIAG2
	LDA $0300,X
	JSR ?PHEX
	LDA #$20
	JSR ?PUTCHR
	INC ?DIAGBT
	BNE ?PDIAG1
?PDIAG2	LDA #155
	JSR ?PUTCHR

	LDA #0
	STA ?DIAGBT
?PDIAG3	LDX ?DIAGBT
	CPX ?DIAGER
	BEQ ?PDIAG0
	LDA ?DIAGS,X
	JSR ?PHEX
	LDA #$20
	JSR ?PUTCHR
	LDX ?DIAGBT
	LDA ?DIAGE,X
	JSR ?PHEX
	LDA #$20
	JSR ?PUTCHR
	LDX ?DIAGBT
	LDA ?DIAGB,X
	JSR ?PHEX
	LDA #155
	JSR ?PUTCHR
	INC ?DIAGBT
	BNE ?PDIAG3

?PDIAG0	RTS

?PHEX	PHA
	LSR
	LSR
	LSR
	LSR
	TAX
	LDA ?HEXTAB,X
	JSR ?PUTCHR
	PLA
	AND #$0F
	TAX
	LDA ?HEXTAB,X

?PUTCHR	TAY
	LDA $E407
	PHA
	LDA $E406
	PHA
	TYA
	RTS

?HEXTAB	.BYTE "0123456789ABCDEF"

.endif

