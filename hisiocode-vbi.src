;  highsiocode-vbi.src - highspeed SIO routine, optional fast VBI code
;
;  Copyright (c) 2003-2009 by Matthias Reichl <hias@horus.com>
;
;  This program is free software; you can redistribute it and/or modify
;  it under the terms of the GNU General Public License as published by
;  the Free Software Foundation; either version 2 of the License, or
;  (at your option) any later version.
;
;  This program is distributed in the hope that it will be useful,
;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;  GNU General Public License for more details.
;
;  You should have received a copy of the GNU General Public License
;  along with this program; if not, write to the Free Software
;  Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.


.if .def FASTVBI

VBICODE 
.if .not .def FASTVBI_NOCLOCK
	INC 20
	BNE ?NOC1
	INC 19
	BNE ?NOC1
	INC 18
.endif

?NOC1	LDA CDTMV1	; check lo byte. if its != 0 timer is running and it must be decremented.
	BNE ?TIDEC	; go decrement, hi byte unchanged because lo != 0
	LDA CDTMV1+1	; this branch: lo = 0
	BEQ ?TIEXIT	; timer is zero (disabled, expired), exit from here
	DEC CDTMV1+1
?TIDEC	DEC CDTMV1
	BEQ ?TICHK	; lo reached 0, check if we hi also reached 0
?TIEXIT	JMP $E462	; timer still running or expired
?TICHK	LDA CDTMV1+1
	BEQ ?RUNT1	; hi is also 0, execute timer vector
	JMP $E462

?RUNT1
?ABSF04	JSR ?RUNT11
	JMP $E462
?RUNT11	JMP (CDTMA1)

?NOCRIT	JMP $E45F

.if .def RELOCTABLE
?VBIADR
?ABSF05	.WORD VBICODE
.endif
.endif
