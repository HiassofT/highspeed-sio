; disk transfer speed test
; time reading sectors 256-511

	.include "cio.inc"
	.include "hisio.inc"

.bank

START	= $2000

	* = START

	LDA #$31
	STA $300
	LDA #1
	STA $301
	LDA #7
	STA $306

	LDA #$52
	STA $302

	LDA #0
	STA DBYTLO
	LDA #1
	STA DBYTHI

	LDA #<BUF
	STA DBUFLO
	LDA #>BUF
	STA DBUFHI

	LDA #0
	STA DAUX1
	LDA #1
	STA DAUX2

TLOOP	LDA $D40B
	BNE TLOOP
	STA $D40A
	LDA #0
	STA 20
	STA 19
	STA 18

TLOOP1	LDA #$40
	STA $303
	JSR $E459
	BPL TOK

	PRINT 0,TERROR
	JMP TLOOP

TOK	INC DAUX1
	BNE TLOOP1

	LDA $D40B
	STA VLINE
	LDA 20
	STA VTIM
	LDA 19
	STA VTIM+1
	LDA 18
	STA VTIM+2

	JSR PHEX
	LDA VTIM+1
	JSR PHEX
	LDA VTIM
	JSR PHEX
	LDA #'.
	JSR PUTCHAR
	LDA VLINE
	JSR PHEX
	LDA #155
	JSR PUTCHAR

	JMP TLOOP

PHEX	PHA
	LSR
	LSR
	LSR
	LSR
	TAX
	LDA HEXTAB,X
	JSR PUTCHAR
	PLA
	AND #$0F
	TAX
	LDA HEXTAB,X

PUTCHAR	TAY
	LDA $E407
	PHA
	LDA $E406
	PHA
	TYA
	RTS

HEXTAB	.BYTE "0123456789ABCDEF"

TERROR	.BYTE "SIO error, restarting test", 155

VLINE	.BYTE 0
VTIM	.BYTE 0,0,0

BUF	= *

.bank

	* = $2E0
	.WORD START
